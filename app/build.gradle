apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "net.kivitro.kittycat"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 3
        versionName "1.0"
        buildConfigField "String", "THE_CAT_API_KEY", getPropertyValue("THE_CAT_API_KEY")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE' // will not include NOTICE file
        exclude 'META-INF/LICENSE' // will not include LICENSE file
    }

}

def getPropertyValue(String key) {
    Properties properties = new Properties()
    properties.load(new FileInputStream("gradle.properties"))
    return "\"" + properties.getProperty(key) + "\""
}

dependencies {
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:palette-v7:$support_version"
    compile "com.android.support:design:$support_version"

    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.1'

    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"
    compile("com.squareup.retrofit2:converter-simplexml:$retrofit_version") {
        exclude module: 'xpp3'
        exclude group: 'stax'
    }
    compile "com.squareup.picasso:picasso:$picasso_version"

    compile "com.squareup.okhttp3:okhttp:$okhttp3_version"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"

    compile "com.jakewharton:kotterknife:$kotterknife_version"

    compile "com.jakewharton.timber:timber:$timber_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.github.chrisbanes:PhotoView:$photoview_version"

    compile("com.mikepenz:aboutlibraries:$about_version@aar") {
        transitive = true
    }

    compile "com.fivehundredpx:greedo-layout:$greedo_version"

    // Unit Tests
    testCompile 'junit:junit:4.12'

    // Integrations Tests
    androidTestCompile 'junit:junit:4.12'
}

buildscript {
    ext.kotlin_version = '1.0.1'
    ext.support_version = '23.2.0'
    ext.retrofit_version = '2.0.0'
    ext.okhttp3_version = '3.2.0'
    ext.picasso_version = '2.5.2'
    ext.kotterknife_version = '0.1.0-SNAPSHOT'
    ext.photoview_version = '1.2.6'
    ext.timber_version = '4.1.2'
    ext.greedo_version = '1.0.0'
    ext.about_version = '5.7.1'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

kapt {
    generateStubs = true
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url 'https://github.com/500px/greedo-layout-for-android/raw/master/releases/'
    }
}
